apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'
apply from: '../versions.gradle'

android {
  kotlinOptions { jvmTarget = "1.8" }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId "com.michaldrabik.showly2"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode versions.versionCode
    versionName versions.versionName
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    def keystorePropertiesFile = rootProject.file("app/keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    release {
      storeFile file("keystore")
      storePassword keystoreProperties['storePassword']
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      versionNameSuffix '-debug'
    }
    release {
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      firebaseAppDistribution {
        serviceCredentialsFile = rootProject.file("app/firebase-service-key.json")
        releaseNotes = ""
        groups = "developers"
      }
    }
  }
}

dependencies {
  implementation project(':common')
  implementation project(':network')
  implementation project(':storage')
  implementation project(':ui-base')
  implementation project(':ui-model')
  implementation project(':ui-repository')
  implementation project(':ui-navigation')
  implementation project(':ui-trakt-sync')
  implementation project(':ui-show')
  implementation project(':ui-search')
  implementation project(':ui-statistics')
  implementation project(':ui-settings')

  implementation "androidx.appcompat:appcompat:$versions.androidAppCompat"
  implementation "androidx.core:core-ktx:$versions.androidKtx"
  implementation "androidx.constraintlayout:constraintlayout:$versions.androidConstraintLayout"
  implementation "androidx.gridlayout:gridlayout:$versions.androidGridLayout"
  implementation "androidx.lifecycle:lifecycle-extensions:$versions.androidLifecycle"
  implementation "androidx.recyclerview:recyclerview:$versions.androidRecycler"
  implementation "androidx.swiperefreshlayout:swiperefreshlayout:$versions.androidSwipeRefresh"
  implementation "androidx.transition:transition:$versions.androidTransition"
  implementation "androidx.work:work-runtime-ktx:$versions.androidWork"
  implementation "androidx.viewpager2:viewpager2:$versions.androidViewPager"
  implementation "androidx.dynamicanimation:dynamicanimation:$versions.androidDynamicAnimation"
  implementation "androidx.navigation:navigation-fragment-ktx:$versions.androidNavigation"
  implementation "androidx.navigation:navigation-ui-ktx:$versions.androidNavigation"
  implementation "com.google.android.play:core:$versions.androidPlayCore"

  implementation "me.relex:circleindicator:$versions.circleIndicator"
  implementation "com.google.android.material:material:$versions.androidMaterial"
  implementation "com.jakewharton.threetenabp:threetenabp:$versions.threeTenAbp"

  implementation "com.google.firebase:firebase-analytics-ktx:$versions.firebaseAnalytics"
  implementation "com.google.firebase:firebase-messaging:$versions.firebaseMessaging"
  implementation "com.google.firebase:firebase-crashlytics:$versions.crashlytics"

  implementation "com.github.bumptech.glide:glide:$versions.glide"
  kapt "com.github.bumptech.glide:compiler:$versions.glide"

  implementation "com.google.dagger:dagger:$versions.dagger"
  kapt "com.google.dagger:dagger-compiler:$versions.dagger"
  kapt "com.google.dagger:dagger-android-processor:$versions.dagger"

  debugImplementation "com.amitshekhar.android:debug-db:$versions.debugDb"
  implementation "com.jakewharton.timber:timber:$versions.timber"

  testImplementation "junit:junit:$versions.junit"
  testImplementation "io.mockk:mockk:$versions.mockk"
  testImplementation "com.google.truth:truth:$versions.truth"
  testImplementation "androidx.arch.core:core-testing:$versions.coreTesting"
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutinesTesting"
  androidTestImplementation "androidx.test:runner:$versions.testRunner"
}

apply plugin: 'com.google.gms.google-services'
