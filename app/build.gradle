apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'io.fabric'

android {
  kotlinOptions { jvmTarget = "1.8" }

  compileSdkVersion 29

  defaultConfig {
    applicationId "com.michaldrabik.showly2"
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 45
    versionName "1.1.3"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    def keystorePropertiesFile = rootProject.file("app/keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    release {
      storeFile file("keystore")
      storePassword keystoreProperties['storePassword']
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      versionNameSuffix '-debug'
    }
    release {
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      firebaseAppDistribution {
        serviceCredentialsFile = rootProject.file("app/firebase-service-key.json")
        releaseNotes = ""
        groups = "developers"
      }
    }
  }
}

dependencies {
  implementation project(':network')
  implementation project(':storage')

  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.core:core-ktx:1.2.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation 'androidx.gridlayout:gridlayout:1.0.0'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
  implementation 'androidx.transition:transition:1.3.0'
  implementation "androidx.work:work-runtime-ktx:2.3.1"
  implementation 'androidx.viewpager2:viewpager2:1.0.0'
  implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
  implementation 'me.relex:circleindicator:2.1.4'
  implementation 'com.google.android.material:material:1.2.0-alpha04'
  implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

  implementation 'com.google.firebase:firebase-analytics:17.2.2'
  implementation 'com.google.firebase:firebase-messaging:20.1.0'
  implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

  implementation "androidx.navigation:navigation-fragment-ktx:2.3.0-alpha01"
  implementation "androidx.navigation:navigation-ui-ktx:2.3.0-alpha01"

  implementation 'com.github.bumptech.glide:glide:4.10.0'
  kapt 'com.github.bumptech.glide:compiler:4.10.0'

  implementation "com.google.dagger:dagger:2.25.2"
  kapt "com.google.dagger:dagger-compiler:2.25.2"
  kapt 'com.google.dagger:dagger-android-processor:2.25.2'

  debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

  testImplementation 'junit:junit:4.12'
  testImplementation 'io.mockk:mockk:1.9.3'
  testImplementation 'com.google.truth:truth:1.0.1'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
